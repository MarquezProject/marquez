plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '4.0.3'
  id "com.diffplug.gradle.spotless" version '3.16.0'
  id 'checkstyle'
  id 'jacoco'
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'io.dropwizard.modules:dropwizard-flyway:1.3.0-4'
  compile 'io.dropwizard:dropwizard-core:1.3.7'
  compile 'io.dropwizard:dropwizard-jdbi3:1.3.7'
  compile 'org.jdbi:jdbi3-postgres:3.4.0'
  compile 'org.jdbi:jdbi3-sqlobject:3.4.0'
  compile 'org.postgresql:postgresql:42.2.4'

  testCompile 'io.dropwizard:dropwizard-testing:1.3.7'
  testCompile 'junit:junit:4.12'
  testCompile 'org.jdbi:jdbi3-testing:3.3.0'
  testCompile 'org.mockito:mockito-core:2.21.0'
}

compileJava {
  options.incremental = true
}

compileTestJava {
  options.incremental = true
}

mainClassName = 'marquez.MarquezApp'

shadowJar {
  transform(com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer)
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

runShadow {
  args = ['server', 'config.yml']
}

spotless {
  java {
    googleJavaFormat()
    removeUnusedImports()
  }
}

def reportsDir = "${buildDir}/reports";

checkstyle {
  toolVersion = '8.12'
  configFile = rootProject.file('checkstyle.xml')
}

task checkstyle(type: Checkstyle) {
  reports {
    xml.enabled = true
    html.enabled = true
    html.destination = file("${reportsDir}/checkstyle")
  }
}

checkstyleMain {
  source = 'src/main/java'
}

checkstyleTest {
  source = 'src/test/java'
}

def coverageDir = "${reportsDir}/coverage";

jacoco {
  toolVersion = '0.8.1'
  reportsDir = file(coverageDir)
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
    html.destination = file(coverageDir)
  }
}
