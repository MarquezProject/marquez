import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'java'
  id 'com.adarshr.test-logger' version '1.6.0'
  id 'com.diffplug.gradle.spotless' version '3.18.0'
  id 'com.github.jk1.dependency-license-report' version '1.3'
  id 'com.github.johnrengelman.shadow' version '4.0.4'
  id 'net.researchgate.release' version '2.6.0'
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'io.dropwizard.modules:dropwizard-flyway:1.3.0-4'
  compile 'io.dropwizard:dropwizard-core:1.3.9'
  compile 'io.dropwizard:dropwizard-jdbi3:1.3.9'
  compile 'io.dropwizard.metrics:metrics-jdbi3:4.1.0-rc3'
  compile 'org.jdbi:jdbi3-postgres:3.6.0'
  compile 'org.jdbi:jdbi3-sqlobject:3.6.0'
  compile 'org.postgresql:postgresql:42.2.5'
  compileOnly 'org.projectlombok:lombok:1.18.4'

  testCompile 'junit:junit:4.12'
  testCompile 'io.dropwizard:dropwizard-testing:1.3.9'
  testCompile 'org.jdbi:jdbi3-testing:3.3.0'
  testCompile 'org.mockito:mockito-core:2.23.4'
}

compileJava {
  options.incremental = true
}

compileTestJava {
  options.incremental = true
}

task testUnit(type: Test) {  
  useJUnit {
    includeCategories 'marquez.UnitTests'
  }
}

mainClassName = 'marquez.MarquezApp'

shadowJar {
  baseName = project.name
  classifier = ''
  version = project.version
  transform(ServiceFileTransformer)
  manifest {
    attributes(
      'Created-By': "Gradle ${gradle.gradleVersion}",
      'Built-By': System.getProperty('user.name'),
      'Build-Jdk': System.getProperty('java.version'),
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
      'Main-Class': mainClassName)
  }
}

runShadow {
  args = ['server', 'config.yml']
}

spotless {
  java {
    googleJavaFormat()
    removeUnusedImports()
  }
}

def reportsDir = "${buildDir}/reports";

checkstyle {
  toolVersion = '8.12'
  configFile = rootProject.file('checkstyle.xml')
}

task checkstyle(type: Checkstyle) {
  reports {
    xml.enabled = true
    html.enabled = true
    html.destination = file("${reportsDir}/checkstyle")
  }
}

checkstyleMain {
  source = 'src/main/java'
}

checkstyleTest {
  source = 'src/test/java'
}

def coverageDir = "${reportsDir}/coverage";

jacoco {
  toolVersion = '0.8.2'
  reportsDir = file(coverageDir)
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
    html.destination = file(coverageDir)
  }
}
