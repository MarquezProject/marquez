/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.adarshr:gradle-test-logger-plugin:2.1.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.6.0'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.10.2'
        classpath 'net.researchgate:gradle-release:2.8.1'
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.github.spotbugs'
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'net.researchgate.release'

    project(':api') {
        apply plugin: 'application'
        archivesBaseName = 'marquez-api'
    }

    project(':clients:java') {
        apply plugin: 'java-library'
        archivesBaseName = 'marquez-java'
    }

    project(':integrations:spark') {
        apply plugin: 'java-library'
        archivesBaseName = 'marquez-spark'
    }

    ext {
        lombokVersion = '1.18.16'
        slf4jVersion = '1.7.26'
        isReleaseVersion = !version.endsWith("SNAPSHOT")
    }

    compileJava {
        options.incremental = true
        options.compilerArgs << '-parameters'
        options.encoding = "UTF-8"
    }

    compileTestJava {
        options.incremental = true
        options.compilerArgs << '-parameters'
        options.encoding = "UTF-8"
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }

    release {
        git {
            requireBranch = 'main'
        }
    }

    spotless {
        java {
            googleJavaFormat()
            removeUnusedImports()
        }
    }

    def reportsDir = "${buildDir}/reports";
    def checkstyleDir = "${reportsDir}/checkstyle";

    checkstyle {
        toolVersion = '8.12'
        configFile = rootProject.file('checkstyle.xml')
    }

    task checkstyle(type: Checkstyle) {
        reports {
            xml.enabled = true
            html.enabled = true
            html.destination = file(checkstyleDir)
        }
    }

    checkstyleMain {
        source = 'src/main/java'
    }

    checkstyleTest {
        source = 'src/test/java'
    }

    spotbugsMain {
        ignoreFailures = true
        effort = 'max'
        reportLevel = 'low'
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    spotbugsTest {
        ignoreFailures = true
        effort = 'max'
        reportLevel = 'low'
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    def coverageDir = "${reportsDir}/coverage";

    jacoco {
        toolVersion = '0.8.5'
        reportsDir = file(coverageDir)
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
            html.destination = file(coverageDir)
        }
    }
}
