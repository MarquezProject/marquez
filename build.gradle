plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  id 'jacoco'
  id 'java'
}

repositories {
  mavenCentral()
}

compileJava {
  options.incremental = true
}

compileTestJava {
  options.incremental = true
}

dependencies {
  compile 'io.dropwizard.modules:dropwizard-flyway:1.3.0-4'
  compile 'io.dropwizard:dropwizard-core:1.3.5'
  compile 'io.dropwizard:dropwizard-jdbi3:1.3.5'
  compile 'org.jdbi:jdbi3-postgres:3.3.0'
  compile 'org.jdbi:jdbi3-sqlobject:3.3.0'
  compile 'org.postgresql:postgresql:42.2.4'

  testCompile 'io.dropwizard:dropwizard-testing:1.3.5'
  testCompile 'org.mockito:mockito-core:2.21.0'
}

mainClassName = 'marquez.MarquezApplication'

shadowJar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

runShadow {
  args = ['server', 'config.yaml']
}

task runDbMigration(type: JavaExec) {
  group = 'Database'
  description = 'Migrates the database.'
  classpath = sourceSets.main.runtimeClasspath
  main = mainClassName
  args = ['db', 'migrate', 'config.yaml']
}

def reportsDir = "$buildDir/reports/coverage";

jacoco {
  toolVersion = "0.8.1"
  reportsDir = file(reportsDir)
}

jacocoTestReport {
  reports {
    html.destination file(reportsDir)
  }
}
