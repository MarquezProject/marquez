/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
    id 'signing'
    id 'application'
    id 'com.github.johnrengelman.shadow'
}

ext {
    jdbi3Version = '3.40.0'
    prometheusVersion = '0.16.0'
    testcontainersVersion = '1.18.3'
    sentryVersion = '6.34.0'
}

dependencies {
    implementation project(':clients:java')
    implementation "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-jdbi3:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-json-logging:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-http2:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-assets:${dropwizardVersion}"
    
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    
    // Jakarta EE dependencies
    implementation platform("jakarta.platform:jakarta.jakartaee-bom:${jakartaVersion}")
    implementation "jakarta.platform:jakarta.jakartaee-api:${jakartaVersion}"
    implementation "jakarta.validation:jakarta.validation-api:${jakartaValidationVersion}"
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'jakarta.transaction:jakarta.transaction-api:2.0.1'
    implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'org.glassfish:jakarta.el:4.0.2'
    
    // GraphQL dependencies with Jakarta EE 9 support
    implementation "com.graphql-java:graphql-java:${graphqlJavaVersion}"
    implementation "com.graphql-java-kickstart:graphql-java-servlet:${graphqlServletVersion}"
    implementation "com.graphql-java-kickstart:graphql-java-kickstart:${graphqlServletVersion}"
    
    implementation "io.prometheus:simpleclient_servlet_jakarta:0.16.0"
    implementation "io.prometheus:simpleclient_common:0.16.0"
    implementation "io.prometheus:simpleclient_dropwizard:0.16.0"
    implementation "io.prometheus:simpleclient_hotspot:0.16.0"
    
    implementation "org.jdbi:jdbi3-core:${jdbi3Version}"
    implementation "org.jdbi:jdbi3-jackson2:${jdbi3Version}"
    implementation "org.jdbi:jdbi3-postgres:${jdbi3Version}"
    implementation "org.jdbi:jdbi3-sqlobject:${jdbi3Version}"
    implementation "io.dropwizard.metrics:metrics-jdbi3:4.2.25"
    implementation 'com.google.guava:guava:32.1.3-jre'
    implementation 'org.dhatim:dropwizard-sentry:2.1.6'
    implementation "io.sentry:sentry:${sentryVersion}"
    implementation 'org.flywaydb:flyway-core:8.5.13'
    implementation "org.postgresql:postgresql:${postgresqlVersion}"
    implementation "io.openlineage:openlineage-java:0.30.1"

    implementation 'org.opensearch.client:opensearch-rest-client:2.19.1'
    implementation 'org.opensearch.client:opensearch-java:2.22.0'

    testImplementation "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    testImplementation "io.dropwizard:dropwizard-jdbi3:${dropwizardVersion}"
    testImplementation "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testImplementation "org.jdbi:jdbi3-testing:${jdbi3Version}"
    testImplementation "org.jdbi:jdbi3-testcontainers:${jdbi3Version}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junit5Version}"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5Version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit5Version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5Version}"
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    
    // Add Jakarta EE dependencies for tests
    testImplementation platform("jakarta.platform:jakarta.jakartaee-bom:${jakartaVersion}")
    testImplementation "jakarta.platform:jakarta.jakartaee-api:${jakartaVersion}"
    testImplementation "jakarta.validation:jakarta.validation-api:${jakartaValidationVersion}"
    testImplementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    testImplementation 'jakarta.transaction:jakarta.transaction-api:2.0.1'
    testImplementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    testImplementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
    testImplementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    testImplementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
}

task testUnit(type: Test) {
    useJUnitPlatform {
        includeTags 'UnitTests'
    }
}

task testIntegration(type: Test) {
    useJUnitPlatform {
        includeTags 'IntegrationTests'
    }
}

task testDataAccess(type: Test) {
    useJUnitPlatform {
        includeTags 'DataAccessTests'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.marquezproject'
            artifactId = 'marquez-api'

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'marquez-api'
                description = 'Collect, aggregate, and visualize a data ecosystem\'s metadata'
                url = 'https://github.com/MarquezProject/marquez'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'marquezproject'
                        name = 'Marquez Project'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/MarquezProject/marquez.git'
                    developerConnection = 'scm:git:ssh://github.com:MarquezProject/marquez.git'
                    url = 'https://github.com/MarquezProject/marquez'
                }
            }
        }
    }

    processResources {
        filter ReplaceTokens, tokens: [
                "version": project.property("version")
        ]
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

application {
    mainClass = 'marquez.MarquezApp'
}

shadowJar {
    archiveClassifier.set('')
    transform(ServiceFileTransformer)
    // Add LICENSE to Jar
    from(projectDir) {
        include 'LICENSE'
    }
    mergeServiceFiles()
    dependencies {
        include(dependency('jakarta.platform:jakarta.jakartaee-api'))
        include(dependency('jakarta.platform:jakarta.jakartaee-bom'))
        include(dependency('jakarta.annotation:jakarta.annotation-api'))
        include(dependency('jakarta.validation:jakarta.validation-api'))
        include(dependency('io.dropwizard:dropwizard-jdbi3'))
    }
    manifest {
        attributes(
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version'),
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': application.mainClass)
    }
}

runShadow {
    args = ['server', '../marquez.yml']
}

tasks.distZip.dependsOn tasks.shadowJar
tasks.distTar.dependsOn tasks.shadowJar
tasks.startScripts.dependsOn tasks.shadowJar
tasks.shadowJar.dependsOn tasks.jar
